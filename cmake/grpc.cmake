message(STATUS "${proj}.cmake with CMAKE_BUILD_TYPE:'${CMAKE_BUILD_TYPE}'")

if(NOT TARGET download-${proj})
  ExternalProject_Add(download-${proj}
    DOWNLOAD_DIR ${DOWNLOAD_DIR}
    URL ${${proj}_url}
    URL_MD5 ${${proj}_md5}
    DOWNLOAD_EXTRACT_TIMESTAMP FALSE
    SOURCE_DIR ${SOURCE_DIR}
    CONFIGURE_COMMAND ""
    BUILD_COMMAND ""
    INSTALL_COMMAND ""
  )
endif()

ExternalProject_Add(${proj}${CMAKE_BUILD_TYPE}
  DOWNLOAD_COMMAND ""
  DOWNLOAD_EXTRACT_TIMESTAMP FALSE
  SOURCE_DIR ${SOURCE_DIR}
  BINARY_DIR ${BINARY_DIR}
  INSTALL_DIR ${INSTALL_DIR}
  CMAKE_ARGS
    # generic args
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX:PATH=${INSTALL_DIR}
    -DCMAKE_CXX_STANDARD_REQUIRED:BOOL=ON
    -DCMAKE_CXX_STANDARD:STRING=17
    -DCMAKE_MSVC_RUNTIME_LIBRARY=${CMAKE_MSVC_RUNTIME_LIBRARY}
    # project specific args for grpc
    -DCMAKE_PREFIX_PATH="${INSTALL_DIR}/lib/cmake/"
    -DBUILD_SHARED_LIBS:BOOL=${BUILD_SHARED_LIBS}
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    -DgRPC_INSTALL=ON
    -DgRPC_BUILD_CODEGEN=ON
    -DgRPC_BUILD_GRPC_CPP_PLUGIN=ON
    -DgRPC_BUILD_TESTS=OFF
    -DgRPC_ABSL_PROVIDER=package
    -DgRPC_PROTOBUF_PROVIDER=package
    -DgRPC_ZLIB_PROVIDER=package
    -DgRPC_CARES_PROVIDER=package
    -DgRPC_RE2_PROVIDER=package
    -DgRPC_SSL_PROVIDER=package
  DEPENDS protobuf${CMAKE_BUILD_TYPE} boringssl${CMAKE_BUILD_TYPE} cares${CMAKE_BUILD_TYPE} re2${CMAKE_BUILD_TYPE}
  UPDATE_COMMAND ""
)
