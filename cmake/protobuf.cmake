message(STATUS "${proj}.cmake with CMAKE_BUILD_TYPE:'${CMAKE_BUILD_TYPE}'")

if(NOT TARGET download-${proj})
  ExternalProject_Add(download-${proj}
    DOWNLOAD_DIR ${DOWNLOAD_DIR}
    URL ${${proj}_url}
    URL_MD5 ${${proj}_md5}
    DOWNLOAD_EXTRACT_TIMESTAMP FALSE
    SOURCE_DIR ${SOURCE_DIR}
    CONFIGURE_COMMAND ""
    BUILD_COMMAND ""
    INSTALL_COMMAND ""
  )
endif()

ExternalProject_Add(${proj}${CMAKE_BUILD_TYPE}
  DOWNLOAD_COMMAND ""
  DOWNLOAD_EXTRACT_TIMESTAMP FALSE
  SOURCE_DIR ${SOURCE_DIR}
  BINARY_DIR ${BINARY_DIR}
  INSTALL_DIR ${INSTALL_DIR}
  CMAKE_ARGS
    # generic args
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX:PATH=${INSTALL_DIR}
    -DCMAKE_CXX_STANDARD_REQUIRED:BOOL=ON
    -DCMAKE_CXX_STANDARD:STRING=17
    -DCMAKE_MSVC_RUNTIME_LIBRARY=${CMAKE_MSVC_RUNTIME_LIBRARY}
    # project specific args for protobuf
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    -Dprotobuf_BUILD_TESTS:BOOL=OFF
    -Dprotobuf_BUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}
    -DCMAKE_PREFIX_PATH=${INSTALL_DIR}
    -Dabsl_DIR="${PROJECT_BUILD_DIR}/x64/${CMAKE_BUILD_TYPE}/lib/cmake/absl/"
    -Dprotobuf_LOCAL_DEPENDENCIES_ONLY:BOOL=ON
    -Dprotobuf_WITH_ZLIB:BOOL=ON
    -Dprotobuf_BUILD_LIBUPB:BOOL=OFF
    -Dprotobuf_MSVC_STATIC_RUNTIME:BOOL=${CMAKE_MSVC_STATIC_RUNTIME}
  DEPENDS abseil-cpp${CMAKE_BUILD_TYPE} zlib${CMAKE_BUILD_TYPE}
  UPDATE_COMMAND ""
)
